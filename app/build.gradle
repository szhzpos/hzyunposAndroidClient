apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin:'kotlin-kapt'
android {
    compileSdkVersion 30
    buildToolsVersion "30.0.3"
    defaultConfig {
        applicationId "com.wyc.cloudapp"
        minSdkVersion 23
        targetSdkVersion 30
        versionCode 16
        versionName "1.1.8"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        externalNativeBuild {
            cmake {
                cppFlags "-std=c++11"
                //生成多个版本的so文件
                abiFilters 'arm64-v8a','armeabi-v7a','x86','x86_64'
            }
        }

        javaCompileOptions {
            annotationProcessorOptions {
                arguments += [
                        "room.schemaLocation":"$projectDir/schemas".toString(),
                        "room.incremental":"true",
                        "room.expandProjection":"true"]
            }
        }
    }

    final Properties props = new Properties()
    props.load(new FileInputStream(file("signing.properties")))
    signingConfigs {
        release{
            keyAlias props['KEY_ALIAS']
            keyPassword props['KEY_PASSWORD']
            storeFile file(props['KEYSTORE_FILE'])
            storePassword props['KEYSTORE_PASSWORD']
        }
    }
    buildTypes {
        release {
            signingConfig signingConfigs.release

            lintOptions {
                checkReleaseBuilds true
                abortOnError true
            }

            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }

        debug {
            signingConfig signingConfigs.release
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    externalNativeBuild {
        cmake {
            path "src/main/java/jni/CMakeLists.txt"
            version "3.10.2"
        }
    }

    sourceSets.main {
        jniLibs.srcDir 'libs'
    }
    applicationVariants.all {
        variant ->
            variant.outputs.all {
                //在这里修改apk文件名
                outputFileName = "${"yunpos_pad_" + defaultConfig.versionCode + "_v" + defaultConfig.versionName}.apk"
                if (variant.buildType.name == 'release') {
                    variant.assembleProvider.get().doLast {
                        copy {
                            duplicatesStrategy = 'include'
                            from variant.getPackageApplicationProvider().get().outputDirectory.getAsFile().get().getPath() + File.separator
                            into "${projectDir}/release"
                            rename { String fileName ->outputFileName}
                        }
                    }
                }
            }
    }

    dataBinding{
        android.buildFeatures.dataBinding = true
    }

    //Jetpack Compose start
    buildFeatures {
        compose true
        viewBinding true
    }
    kotlinOptions {
        jvmTarget = "1.8"
    }
    composeOptions {
        kotlinCompilerExtensionVersion '1.0.5'
    }
    //Jetpack Compose end
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'


    implementation 'org.jetbrains:annotations:15.0'
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'

    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
    implementation 'net.sourceforge.jexcelapi:jxl:2.6.12'
    implementation 'androidx.recyclerview:recyclerview:1.2.0'
    implementation 'com.google.android.material:material:1.3.0'
    implementation project(path:':android')
    implementation 'com.alibaba:fastjson:1.2.72'
    implementation 'com.squareup.okhttp3:okhttp:3.10.0'

    debugApi 'com.squareup.leakcanary:leakcanary-android:1.6'
    releaseApi 'com.squareup.leakcanary:leakcanary-android-no-op:1.6'
    testImplementation 'com.squareup.leakcanary:leakcanary-android-no-op:1.6'

    implementation 'com.jakewharton:butterknife:10.1.0'
    kapt 'com.jakewharton:butterknife-compiler:10.1.0'

    implementation 'org.greenrobot:eventbus:3.1.1'

    implementation 'io.reactivex.rxjava3:rxandroid:3.0.0'
    implementation 'io.reactivex.rxjava3:rxjava:3.0.0'

    implementation 'com.github.getActivity:XXPermissions:11.5'

    //room database
    def room_version = "2.3.0"
    implementation "androidx.room:room-runtime:$room_version"
    annotationProcessor "androidx.room:room-compiler:$room_version"

    kapt("androidx.room:room-compiler:$room_version")
    implementation("androidx.room:room-ktx:$room_version")
    //end room database

    kapt 'com.github.bumptech.glide:compiler:4.9.0'
    implementation 'com.github.bumptech.glide:glide:4.9.0'

    /* Jetpack Compose start*/
    // Integration with activities
    implementation 'androidx.activity:activity-compose:1.3.1'
    // Compose Material Design
    implementation 'androidx.compose.material:material:1.0.5'
    // Animations
    implementation 'androidx.compose.animation:animation:1.0.5'
    // Tooling support (Previews, etc.)
    implementation 'androidx.compose.ui:ui-tooling:1.0.5'
    // Integration with ViewModels
    implementation 'androidx.lifecycle:lifecycle-viewmodel-compose:1.0.0-alpha07'
    //reuse existing view layout
    implementation 'androidx.compose.ui:ui-viewbinding:1.0.5'
    // UI Tests
    androidTestImplementation 'androidx.compose.ui:ui-test-junit4:1.0.5'
    /* end */

    //托利多一体称
    implementation files('libs/MtWeighApi.jar')
    implementation files('libs/MtPrintApi.jar')

    //托利多bPlus条码秤
    implementation files('libs/mtaoscaleapi.jar')
}
